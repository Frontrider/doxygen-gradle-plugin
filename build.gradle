
buildscript {
    repositories {
        jcenter()
	    mavenLocal()
    }
    dependencies {
        classpath 'org.ysb33r.gradle:bintray:1.5'
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:2.2.3'
    }
}

subprojects {
    apply plugin : 'org.ysb33r.bintray'
    apply plugin : 'groovy'
    apply plugin : 'maven'
    apply plugin : 'idea'
    apply plugin : 'artifactory'


    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    group = 'org.ysb33r.gradle'

    repositories {
        jcenter()
    }

    ext {
        bintrayOwner = 'ysb33r'
        bintrayRepo = 'grysb33r'
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar) {
        description "An archive of the JavaDocs for Maven Central"
        classifier "javadoc"
        from javadoc
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    dependencies {
        compile gradleApi()
        compile localGroovy()
        testCompile ('org.spockframework:spock-core:0.7-groovy-2.0') {
            exclude module : 'groovy-all'
        }
    }

    artifactory {
        publish {
            contextUrl = 'http://oss.jfrog.org'
            repository {
                repoKey = 'oss-snapshot-local'
                username = project.properties.bintrayUserName
                password = project.properties.bintrayApiKey
            }
        }
    }

    artifactoryPublish {
        onlyIf { version.endsWith("SNAPSHOT") }
    }
}


